buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://maven.fabricmc.net/' }
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.4.2'
    }
}

plugins {
    id 'fabric-loom' version '1.6.3'
    id 'maven-publish'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/kawaii.accesswidener")
}

repositories {
    flatDir {
        dirs 'lib'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation name: 'nether-pathfinder-1.4.1'
    modImplementation name: 'IAS-Fabric-1.20.4-9.0.2-SNAPSHOT'
    modImplementation name: 'satin-1.15.0'
    include name: 'satin-1.15.0'
    include name: 'IAS-Fabric-1.20.4-9.0.2-SNAPSHOT'
    include "org.ladysnake:satin:1.15.0"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
    }
}

task copyZkmLibs(type: Copy) {
    into "$buildDir/zkmLibs"
    from configurations.runtimeClasspath
    from(configurations.compileClasspath)
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

import proguard.gradle.ProGuardTask

tasks.register('proguardJar', ProGuardTask) {
    dependsOn tasks.remapJar
    doNotTrackState("ProGuardTask inputs are not fully trackable (e.g. JDK jmods).")

    def inJar = tasks.remapJar.archiveFile.get().asFile
    def outJar = file("$buildDir/libs/${project.archives_base_name}-${project.version}-optimized.jar")

    injars(inJar.absolutePath)
    outjars(outJar.absolutePath)

    def javaHome = System.getProperty('java.home')
    if (System.getProperty('java.version').startsWith('1.')) {
        libraryjars "${javaHome}/lib/rt.jar"
    } else {
        def jmodsDir = file("${javaHome}/jmods")
        if (jmodsDir.exists()) {
            def jmodFiles = fileTree(jmodsDir).matching { include('*.jmod') }.files
            if (!jmodFiles.isEmpty()) {
                libraryjars(files(jmodFiles), jarfilter: '!**.jar', filter: '!module-info.class')
            }
        }
    }

    def mcIntermediaryJars = fileTree("$rootDir/obf-workspace/zkmLibs").matching {
        include("minecraft-merged-intermediary-${project.minecraft_version}-*.jar")
    }.files
    if (!mcIntermediaryJars.isEmpty()) {
        libraryjars(files(mcIntermediaryJars))
    }

    def libJars = new LinkedHashSet<File>()
    libJars.addAll(configurations.compileClasspath.files)
    libJars.addAll(configurations.runtimeClasspath.files)
    libraryjars(files(libJars))

    dontobfuscate()
    ignorewarnings()
    dontwarn('**')

    keepattributes(
            'RuntimeVisibleAnnotations,' +
                    'RuntimeInvisibleAnnotations,' +
                    'RuntimeVisibleParameterAnnotations,' +
                    'RuntimeInvisibleParameterAnnotations,' +
                    'Signature,' +
                    'InnerClasses,' +
                    'EnclosingMethod'
    )

    keep('class dev.kizuna.** { *; }')
    keep('class net.fabricmc.api.** { *; }')
    keep('class net.fabricmc.loader.api.** { *; }')
    keep('class org.spongepowered.asm.mixin.** { *; }')
    keep('class com.mojang.blaze3d.** { *; }')
    keep('class net.minecraft.** { *; }')
}

tasks.assemble.dependsOn(tasks.named('proguardJar'))
